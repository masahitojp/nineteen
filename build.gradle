apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'com.github.masahitojp'
archivesBaseName = "nineteen"
version = "0.0.2"
sourceCompatibility = targetCompatibility = '1.8'
repositories.mavenCentral()

def defaultEncoding = 'UTF-8'
tasks.withType(AbstractCompile)*.options*.encoding = defaultEncoding
tasks.withType(GroovyCompile)*.groovyOptions*.encoding = defaultEncoding

repositories {
    maven {
        // Atilika Open Source repository (for kuromoji)
        url "https://oss.sonatype.org/content/repositories/comatilika-1002"
    }
}

configurations {
    provided
}
 
sourceSets {
    main {
        compileClasspath += configurations.provided
    }
    test {
        compileClasspath += configurations.provided
        runtimeClasspath += configurations.provided
    }
}
 
idea {
    module {
        scopes.PROVIDED.plus += [configurations.provided]
    }
}
 
dependencies {
    compile "com.atilika.kuromoji:kuromoji-ipadic:0.9.0"
    provided "org.projectlombok:lombok:1.16.4"
    testCompile 'junit:junit:4.12'
}

jar.manifest {
    attributes(
            'Implementation-Title' : name,
            'Implementation-Version': version,
            'Implementation-Vendor': 'com.github.masahitojp',
            'Built-JDK': "${System.getProperty("java.version")} (${System.getProperty("java.specification.vendor")})",
            'Built-Gradle': gradle.gradleVersion
    )
}
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}
task javadocJar(type: Jar) {
    classifier = "javadoc"
    from javadoc
}
artifacts {
    archives javadocJar, sourcesJar
}


signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: hasProperty('ossrhUsername')?ossrhUsername:'', password: hasProperty('ossrhPassword')?ossrhPassword:'')
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: hasProperty('ossrhUsername')?ossrhUsername:'', password: hasProperty('ossrhPassword')?ossrhPassword:'')
            }

            pom.project {
                name 'nineteen'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'It is determined whether or not the haiku'
                url 'https://github.com/masahitojp/nineteen'

                scm {
                    url 'https://github.com/masahitojp/nineteen.git'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'masahito'
                        name "Nakamura Masato"
                        email "randomstep@gmail.com"
                    }
                }
            }
        }
    }
}
